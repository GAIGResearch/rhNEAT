BasicGame no_players=2
    SpriteSet
        background > Immovable hidden=True img=oryx/space1
        base > Immovable color=WHITE img=newset/block3
        solidbase > Immovable color=BLUE img=newset/block1

        moving >
            avatar  > ShootAvatar speed=0.9
                avatar1 > stype=sam1 img=oryx/spaceship1
                avatar2 > stype=sam2 img=oryx/spaceship2
            missile > Missile timeToLive=20
                sam1  > color=BLUE singleton=True img=oryx/orb3 shrinkfactor=0.5
                sam2  > color=RED singleton=True img=oryx/orb1 shrinkfactor=0.5

        asteroid >

            static > RandomNPC img=oryx/planet is_npc=False
                big > speed=0.1 cons=1
                medium > speed=0.10 cons=2 shrinkfactor=0.8
                small > speed=0.15 cons=3 shrinkfactor=0.5

            rnd > RandomMissile speed=0.15 img=oryx/planet
                rndBig > speed=0.1
                rndMedium > speed=0.10 shrinkfactor=0.8
                rndSmall > speed=0.15 shrinkfactor=0.5

        portal  > SpawnPoint cooldown=16 invisible=True hidden=True
            portalR > stype=big total=3
            portalH > stype=rndBig total=1

        coin >
            staticC > Immovable img=oryx/coins1
            rndC > RandomMissile speed=0.15 img=oryx/coins1
            specC > Immovable img=oryx/coins2

        coinportal > SpawnPoint cooldown=10 invisible=True hidden=True
            cpR > stype=staticC total=1
            cpH > stype=rndC total=1

        monster > Chaser stype=avatar speed=0.1 img=oryx/alien1

        monsterportal > SpawnPoint cooldown=0 invisible=true hidden=true total=1
            mp > stype=monster


        laser > Missile timeToLive=500 speed=0.15
            laserU > orientation=UP img=newset/laser2
            laserD > orientation=DOWN img=newset/laser2
            laserL > orientation=LEFT img=newset/laser2_1
            laserR > orientation=RIGHT img=newset/laser2_1

        laserSpawn > SpawnPoint cooldown=6 invisible=true hidden=true total=5
            laserSpawnU > stype=laserU
            laserSpawnD > stype=laserD
            laserSpawnL > stype=laserL
            laserSpawnR > stype=laserR

        laserGun > SpawnPoint cooldown=100 img=oryx/wall2 invisible=false hidden=true total=0
            laserGunU > stype=laserSpawnU
            laserGunD > stype=laserSpawnD
            laserGunL > stype=laserSpawnL
            laserGunR > stype=laserSpawnR


    LevelMapping
        p > portalR background
        h > portalH background
        b > base background
        A > avatar1 background
        B > avatar2 background
        . > background
        c > cpR background
        v > cpH background
        m > mp background
        s > solidbase background
        u > laserGunU background
        d > laserGunD background
        l > laserGunL background
        r > laserGunR background

    TerminationSet
        MultiSpriteCounter stype1=avatar1 stype2=avatar2 limit=0 win=False,False
        SpriteCounter stype=avatar1 limit=0 win=False,True
        SpriteCounter stype=avatar2 limit=0 win=True,False
        SpriteCounter stype=asteroid limit=0 count_score=True
        Timeout limit=2000 count_score=True

    InteractionSet
        moving EOS > wrapAround
        rnd EOS > wrapAround
        asteroid EOS > stepBack
        rndC EOS > wrapAround
        monster EOS > wrapAround

        avatar avatar > killBoth
        avatar1 sam2 > killSprite scoreChange=-2,10
        avatar2 sam1 > killSprite scoreChange=10,-2

        missile big > spawn stype=medium
        missile big > spawn stype=medium
        missile big > killBoth
        missile medium > spawn stype=small
        missile medium > spawn stype=small
        missile medium > killBoth

        missile rndBig > spawn stype=rndMedium
        missile rndBig > spawn stype=rndMedium
        missile rndBig > killBoth
        missile rndMedium > spawn stype=rndSmall
        missile rndMedium > spawn stype=rndSmall
        missile rndMedium > killBoth

        sam1 monster > spawn stype=specC scoreChange=3,0
        sam1 monster > killBoth
        sam2 monster > spawn stype=specC scoreChange=0,3
        sam2 monster > killBoth

        sam1 rndSmall small > killBoth scoreChange=3,0
        sam2 rndSmall small > killBoth scoreChange=0,3

        avatar1 asteroid > killBoth scoreChange=-1,0
        avatar2 asteroid > killBoth scoreChange=0,-1

        asteroid base > reverseDirection
        base asteroid > killSprite
        base sam1 > killBoth scoreChange=1,0
        base sam2 > killBoth scoreChange=0,1
        avatar base > stepBack pixelPerfect=True
        avatar solidbase > stepBack pixelPerfect=True
        missile solidbase > killBoth
        asteroid solidbase > reverseDirection pixelPerfect=True
        laser solidbase > killSprite

        staticC avatar1 > killSprite scoreChange=1,0
        staticC avatar2 > killSprite scoreChange=0,1
        rndC avatar1 > killSprite scoreChange=1,0
        rndC avatar2 > killSprite scoreChange=0,1
        specC avatar1 > killSprite scoreChange=3,0
        specC avatar2 > killSprite scoreChange=0,3

        monster avatar1 > killBoth
        monster avatar2 > killBoth

        laser EOS > killSprite
        laser base > killSprite
        laser solidbase > killSprite
        laser avatar1 > killBoth scoreChange=-1,0
        laser avatar2 > killBoth scoreChange=0,-1
        laser big > spawn stype=medium
        laser big > spawn stype=medium
        laser big > killBoth
        laser medium > spawn stype=small
        laser medium > spawn stype=small
        laser medium > killBoth
        laser rndBig > spawn stype=rndMedium
        laser rndBig > spawn stype=rndMedium
        laser rndBig > killBoth
        laser rndMedium > spawn stype=rndSmall
        laser rndMedium > spawn stype=rndSmall
        laser rndMedium > killBoth
        laser rndSmall small > killBoth
        laser rndSmall small > killBoth